
AC_INIT(README)
AC_CANONICAL_SYSTEM
AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(mono, 0.12)
AM_MAINTAINER_MODE

dnl ****************************************
dnl *** Check if we're building on win32 ***
dnl ****************************************
AC_MSG_CHECKING([if building for some Win32 platform])
case "$host" in
	*-*-mingw*|*-*-cygwin*)
		platform_win32=yes
		AC_DEFINE(PLATFORM_WIN32)
		CC="gcc -mno-cygwin"
		;;
	*)
		platform_win32=no
		;;
esac
AC_MSG_RESULT($platform_win32)
AM_CONDITIONAL(PLATFORM_WIN32, test x$platform_win32 = xyes)

AC_CHECK_TOOL(CC, gcc, gcc)
AC_PROG_CC
AM_PROG_CC_STDC
AC_PROG_INSTALL

dnl may require a specific autoconf version
dnl AC_PROG_CC_FOR_BUILD
dnl CC_FOR_BUILD not automatically detected
CC_FOR_BUILD=$CC
BUILD_EXEEXT=
if test "x$cross_compiling" = "xyes"; then
	CC_FOR_BUILD=cc
	BUILD_EXEEXT=""
fi
AC_SUBST(CC_FOR_BUILD)
AC_SUBST(HOST_CC)
AC_SUBST(BUILD_EXEEXT)

# Set STDC_HEADERS
AC_HEADER_STDC
AC_LIBTOOL_WIN32_DLL
AM_PROG_LIBTOOL

AC_CHECK_HEADERS(sys/filio.h sys/sockio.h netdb.h utime.h)

AC_CHECK_HEADERS(iconv.h)
AC_CHECK_HEADERS(giconv.h)

# for mono/metadata/debug-symfile.c
AC_CHECK_HEADERS(elf.h)

# not 64 bit clean in cross-compile
AC_CHECK_SIZEOF(void *, 4)

CFLAGS='-g -Wall -Wunused -Wmissing-prototypes -Wmissing-declarations -Wstrict-prototypes  -Wmissing-prototypes -Wnested-externs -Wpointer-arith -Wno-cast-qual -Wcast-align -Wwrite-strings'

AC_PATH_PROG(PKG_CONFIG, pkg-config, no)
if test "x$PKG_CONFIG" = "xno"; then
	AC_MSG_ERROR([You need to install pkg-config])
fi

dnl for use on the build system
dnl pkg-config is stupid
BUILD_GLIB_CFLAGS=`$PKG_CONFIG --cflags glib-2.0`
BUILD_GLIB_LIBS=`$PKG_CONFIG --libs glib-2.0`
AC_SUBST(BUILD_GLIB_CFLAGS)
AC_SUBST(BUILD_GLIB_LIBS)

PKG_PATH=
AC_ARG_WITH(crosspkgdir, [  --with-crosspkgdir=/path/to/pkg-config/dir],
	if test x$with_crosspkgdir = "x"; then
		if test -s $PKG_CONFIG_PATH; then
			PKG_PATH=$PKG_CONFIG_PATH
		fi
	else
		PKG_PATH=$with_crosspkgdir
		PKG_CONFIG_PATH=$PKG_PATH
		export PKG_CONFIG_PATH
	fi
)

## Versions of dependencies
GLIB_REQUIRED_VERSION=1.3.11

PKG_CHECK_MODULES(BASE_DEPENDENCIES, glib-2.0 >= $GLIB_REQUIRED_VERSION)

GLIB_CFLAGS=`$PKG_CONFIG --cflags glib-2.0`
GLIB_LIBS=`$PKG_CONFIG --libs glib-2.0`
GMODULE_CFLAGS=`$PKG_CONFIG --cflags gmodule-2.0`
GMODULE_LIBS=`$PKG_CONFIG --libs gmodule-2.0`

AC_SUBST(GLIB_CFLAGS)
AC_SUBST(GLIB_LIBS)
AC_SUBST(GMODULE_CFLAGS)
AC_SUBST(GMODULE_LIBS)

AC_CHECK_LIB(gc, GC_malloc, found_boehm="yes")
gc=auto
AC_ARG_WITH(gc, [  --with-gc=boehm,none],[gc=$with_gc])

case "x$gc" in
	xboehm|xyes)
		if test "x$found_boehm" != "xyes"; then
			AC_MSG_ERROR("GC requested but libgc not found!")
		fi

		AC_DEFINE(HAVE_BOEHM_GC)
		AC_SUBST(HAVE_BOEHM_GC)
		LIBS="$LIBS -lgc"
		;;
	xnone)
		AC_MSG_WARN("Compiling mono without GC.")
		;;
	*)
dnl When the JIT works with libgc, use this chunk.
dnl		# If libgc was found, use it.  Otherwise just warn.
		if test "x$found_boehm" != "xyes"; then
			AC_MSG_WARN("Compiling mono without GC.")
		else
			AC_DEFINE(HAVE_BOEHM_GC)
			AC_SUBST(HAVE_BOEHM_GC)
			LIBS="$LIBS -lgc"
			gc="boehm"
		fi
dnl		AC_MSG_WARN("Compiling mono without GC.")
		;;
esac

if test x$platform_win32 = xno; then
	dnl ******************************************************************
	dnl *** Check for large file support                               ***
	dnl *** (If we were using autoconf 2.50 we'd use AC_SYS_LARGEFILE) ***
	dnl ******************************************************************
	
	# Check that off_t can represent 2**63 - 1 correctly, working around
	# potential compiler bugs.  Defines LARGE_FILE_SUPPORT, adds $1 to
	# CPPFLAGS and sets $large_offt to yes if the test succeeds
	large_offt=no
	AC_DEFUN(LARGE_FILES, [
		large_CPPFLAGS=$CPPFLAGS
		CPPFLAGS="$CPPFLAGS $1"
		AC_TRY_RUN([
			#include <sys/types.h>

			#define BIG_OFF_T (((off_t)1<<62)-1+((off_t)1<<62))

			int main(void) {
				int big_off_t=((BIG_OFF_T%2147483629==721) &&
					       (BIG_OFF_T%2147483647==1));
				if(big_off_t) {
					exit(0);
				} else {
					exit(1);
				}
			}
		], [
			AC_MSG_RESULT(ok)
			AC_DEFINE(HAVE_LARGE_FILE_SUPPORT)
			large_CPPFLAGS="$large_CPPFLAGS $1"
			large_offt=yes
		], [
			AC_MSG_RESULT(no)
		], "")
		CPPFLAGS=$large_CPPFLAGS
	])

	AC_MSG_CHECKING(if off_t is 64 bits wide)
	LARGE_FILES("")
	if test $large_offt = no; then
		AC_MSG_CHECKING(if _FILE_OFFSET_BITS=64 gives 64 bit off_t)
		LARGE_FILES("-D_FILE_OFFSET_BITS=64")
	fi
	if test $large_offt = no; then
		AC_MSG_WARN([No 64 bit file size support available])
	fi
	
	dnl *****************************
	dnl *** Checks for libsocket  ***
	dnl *****************************
	AC_CHECK_LIB(socket, socket, LIBS="$LIBS -lsocket")

	dnl *******************************
	dnl *** Checks for MSG_NOSIGNAL ***
	dnl *******************************
	AC_MSG_CHECKING(for MSG_NOSIGNAL)
	AC_TRY_COMPILE([#include <sys/socket.h>], [
		int f = MSG_NOSIGNAL;
	], [
		# Yes, we have it...
		AC_MSG_RESULT(yes)
		AC_DEFINE(HAVE_MSG_NOSIGNAL)
	], [
		# We'll have to use signals
		AC_MSG_RESULT(no)
	])

	dnl *****************************
	dnl *** Checks for SOL_IP     ***
	dnl *****************************
	AC_MSG_CHECKING(for SOL_IP)
	AC_TRY_COMPILE([#include <netdb.h>], [
		int level = SOL_IP;
	], [
		# Yes, we have it...
		AC_MSG_RESULT(yes)
		AC_DEFINE(HAVE_SOL_IP)
	], [
		# We'll have to use getprotobyname
		AC_MSG_RESULT(no)
	])

	dnl *****************************
	dnl *** Checks for SOL_TCP    ***
	dnl *****************************
	AC_MSG_CHECKING(for SOL_TCP)
	AC_TRY_COMPILE([#include <netdb.h>], [
		int level = SOL_TCP;
	], [
		# Yes, we have it...
		AC_MSG_RESULT(yes)
		AC_DEFINE(HAVE_SOL_TCP)
	], [
		# We'll have to use getprotobyname
		AC_MSG_RESULT(no)
	])

	dnl *****************************
	dnl *** Checks for IP_PKTINFO ***
	dnl *****************************
	AC_MSG_CHECKING(for IP_PKTINFO)
	AC_TRY_COMPILE([#include <netdb.h>], [
		int level = IP_PKTINFO;
	], [
		# Yes, we have it...
		AC_MSG_RESULT(yes)
		AC_DEFINE(HAVE_IP_PKTINFO)
	], [
		AC_MSG_RESULT(no)
	])

	dnl *********************************
	dnl *** Check for struct ip_mreqn ***
	dnl *********************************
	AC_MSG_CHECKING(for struct ip_mreqn)
	AC_TRY_COMPILE([#include <netinet/in.h>], [
		struct ip_mreqn mreq;
		mreq.imr_address.s_addr = 0;
	], [
		# Yes, we have it...
		AC_MSG_RESULT(yes)
		AC_DEFINE(HAVE_STRUCT_IP_MREQN)
	], [
		# We'll just have to use struct ip_mreq
		AC_MSG_RESULT(no)
	])

	dnl *****************************
	dnl *** Checks for libnsl     ***
	dnl *****************************
	AC_CHECK_LIB(nsl, gethostbyaddr, LIBS="$LIBS -lnsl")

	AC_CHECK_FUNCS(inet_pton inet_aton)

	dnl ***********************************************
	dnl *** Checks for size of sockaddr_un.sun_path ***
	dnl ***********************************************
	# AC_CHECK_SIZEOF can't cope with struct members :-(
	AC_MSG_CHECKING(size of sockaddr_un.sun_path)
	AC_CACHE_VAL(cv_mono_sizeof_sunpath,
		[AC_TRY_RUN([
			#include <stdio.h>
			#include <sys/un.h>

			int main(void) {
				struct sockaddr_un sock_un;
				FILE *f=fopen("conftestval", "w");
				if(!f) exit(1);
				fprintf(f, "%d\n", sizeof(sock_un.sun_path));
				exit(0);
			}
		], cv_mono_sizeof_sunpath=`cat conftestval`,
		   cv_mono_sizeof_sunpath=0,
		   cv_mono_sizeof_sunpath=0)])dnl
	AC_MSG_RESULT($cv_mono_sizeof_sunpath)
	AC_DEFINE_UNQUOTED(MONO_SIZEOF_SUNPATH, $cv_mono_sizeof_sunpath)

	dnl *****************************
	dnl *** Checks for libpthread ***
	dnl *****************************
	AC_SEARCH_LIBS(pthread_create, pthread c_r, [
		AM_CONDITIONAL(THREADS_PTHREAD, true)
		AC_DEFINE(HAVE_PTHREAD)

		# Need pthread_mutex_timedlock
		pthread_CFLAGS=""
		orig_CPPFLAGS=$CPPFLAGS
		# This is a gcc-specific error, but we already set
		# gcc-specific options in CFLAGS
		CPPFLAGS="$CPPFLAGS -Werror-implicit-function-declaration"
		AC_MSG_CHECKING(for pthread_mutex_timedlock)
		AC_TRY_COMPILE([ #include <pthread.h>], [
			pthread_mutex_t mut=PTHREAD_MUTEX_INITIALIZER;
			pthread_mutex_timedlock(&mut, NULL);
		], [
			# Works!
			AC_MSG_RESULT(yes)
			AC_DEFINE(HAVE_PTHREAD_MUTEX_TIMEDLOCK)
		], [
			AC_MSG_RESULT(no)

			# glibc requires -D_GNU_SOURCE before it will declare
			# this function
			AC_MSG_CHECKING(whether _GNU_SOURCE is needed for pthread_mutex_timedlock)
			CPPFLAGS="$CPPFLAGS -D_GNU_SOURCE"
			AC_TRY_COMPILE([ #include <pthread.h>], [
				pthread_mutex_t mut=PTHREAD_MUTEX_INITIALIZER;
				pthread_mutex_timedlock(&mut, NULL);
			], [
				AC_MSG_RESULT(yes)
				pthread_CFLAGS="-D_GNU_SOURCE"
				AC_DEFINE(HAVE_PTHREAD_MUTEX_TIMEDLOCK)
			], [
				AC_MSG_RESULT(no)
				dnl Add other variants here
				AC_MSG_WARN(Working around pthread_mutex_timedlock)
			])
		])
		CPPFLAGS=$orig_CPPFLAGS
		CPPFLAGS="$CPPFLAGS $pthread_CFLAGS"

		# Need PTHREAD_MUTEX_RECURSIVE
		pthread_CFLAGS=""
		orig_CPPFLAGS=$CPPFLAGS
		# This is a gcc-specific error, but we already set
		# gcc-specific options in CFLAGS
		CPPFLAGS="$CPPFLAGS -Werror-implicit-function-declaration"
		AC_MSG_CHECKING(for PTHREAD_MUTEX_RECURSIVE)
		AC_TRY_COMPILE([ #include <pthread.h>], [
			pthread_mutexattr_t attr;
			pthread_mutexattr_settype(&attr, PTHREAD_MUTEX_RECURSIVE);
		], [
			# Works!
			AC_MSG_RESULT(ok)
		], [
			AC_MSG_RESULT(no)

			# glibc requires -D_GNU_SOURCE before it will declare
			# this macro
			AC_MSG_CHECKING(whether _GNU_SOURCE is needed for PTHREAD_MUTEX_RECURSIVE)
			CPPFLAGS="$CPPFLAGS -D_GNU_SOURCE"
			AC_TRY_COMPILE([ #include <pthread.h>], [
				pthread_mutexattr_t attr;
				pthread_mutexattr_settype(&attr, PTHREAD_MUTEX_RECURSIVE);
			], [
				AC_MSG_RESULT(ok)
				pthread_CFLAGS="-D_GNU_SOURCE"
			], [
				AC_MSG_RESULT(no)
				dnl Add other variants here
				AC_MSG_WARN(Using mono_mutex_t for recursive mutexes)
				AC_DEFINE(USE_MONO_MUTEX)
			])
		])
		CPPFLAGS=$orig_CPPFLAGS
		CPPFLAGS="$CPPFLAGS $pthread_CFLAGS"
	], [
		AC_MSG_ERROR([libpthread is required on non-win32 hosts])
	])

	dnl ********************************
	dnl *** Checks for semaphore lib ***
	dnl ********************************
	AC_CHECK_LIB(rt, sem_init, LIBS="$LIBS -lrt")

	dnl ********************************
	dnl *** Checks for timezone stuff **
	dnl ********************************
	AC_CACHE_CHECK(for timezone variable, ac_cv_var_timezone,
		AC_TRY_COMPILE([
			#include <time.h>
		], [
			timezone = 1;
		], ac_cv_var_timezone=yes, ac_cv_var_timezone=no))
	if test $ac_cv_var_timezone = yes; then
		AC_DEFINE(HAVE_TIMEZONE)
	else
		AC_CACHE_CHECK(for tm_gmtoff in struct tm, ac_cv_struct_tm_gmtoff,
			AC_TRY_COMPILE([
				#include <time.h>
				], [
				struct tm tm;
				tm.tm_gmtoff = 1;
				], ac_cv_struct_tm_gmtoff=yes, ac_cv_struct_tm_gmtoff=no))
		if test $ac_cv_struct_tm_gmtoff = yes; then
			AC_DEFINE(HAVE_TM_GMTOFF)
		else
			AC_ERROR(unable to find a way to determine timezone)
		fi
	fi
else
	AM_CONDITIONAL(THREADS_PTHREAD, false)
	# This is a kludge, we really ought to test for libws2_32, but
	# I can't make AC_CHECK_LIB link (the symbols seem to be mangled
	# with @num suffixes)
	LIBS="$LIBS -lws2_32"
	CFLAGS="$CFLAGS -mno-cygwin"
fi

dnl ****************************
dnl *** Look for /dev/random ***
dnl ****************************

AC_MSG_CHECKING([if usage of random device is requested])
AC_ARG_ENABLE(dev-random,
[  --disable-dev-random    disable the use of the random device],
try_dev_random=$enableval, try_dev_random=yes)
AC_MSG_RESULT($try_dev_random)

case "{$target}" in
    *-openbsd*)
    NAME_DEV_RANDOM="/dev/srandom"
    ;;

dnl Win32 does not have /dev/random, they have their own method...

    *-*-mingw*|*-*-cygwin*)
    ac_cv_have_dev_random = no
    ;;

dnl Everywhere else, it's /dev/random

    *)
    NAME_DEV_RANDOM="/dev/random"
    ;;
esac

AC_DEFINE_UNQUOTED(NAME_DEV_RANDOM, "$NAME_DEV_RANDOM")

dnl Now check if the device actually exists

if test "x$try_dev_random" = "xyes"; then
    AC_CACHE_CHECK(for random device, ac_cv_have_dev_random,
    [if test -r "$NAME_DEV_RANDOM" ; then
        ac_cv_have_dev_random=yes; else ac_cv_have_dev_random=no; fi])
    if test "x$ac_cv_have_dev_random" = "xyes"; then
        AC_DEFINE(HAVE_CRYPT_RNG)
    fi
else
    AC_MSG_CHECKING(for random device)
    ac_cv_have_dev_random=no
    AC_MSG_RESULT(has been disabled)
fi

if test "x$platform_win32" = "xyes"; then
    AC_DEFINE(HAVE_CRYPT_RNG)
fi

if test "x$ac_cv_have_dev_random" = "xno" \
    && test "x$platform_win32" = "xno"; then
    AC_MSG_WARN([[
***
*** A system-provided entropy source was not found on this system.
*** Because of this, the System.Security.Cryptography random number generator
*** will throw a NotImplemented exception.
***
*** If you are seeing this message, and you know your system DOES have an
*** entropy collection in place, please contact <crichton@gimp.org> and
*** provide information about the system and how to access the random device.
***
*** For those systems who lack a random device, EGD support is forthcoming.
***]])
fi
 
if test "x$cross_compiling" = "xno"; then
# check for new iconv version
AC_MSG_CHECKING(for new iconv)
AC_CACHE_VAL(new_iconv,[
	AC_TRY_RUN([#include <stdio.h> 
	        #include <iconv.h>
 
		int main()
		{
		  exit (iconv_open ("UTF-16le", "UTF-8") == (iconv_t)-1);
		}
		],
		new_iconv=yes,new_iconv=)])
if test -n "$new_iconv"; then
   	AC_MSG_RESULT(yes)
	AC_DEFINE(HAVE_NEW_ICONV)
else
	AC_MSG_RESULT(no)
fi
fi

AC_MSG_CHECKING([if inter-process shared handles are requested])
AC_ARG_ENABLE(shared-handles, [  --disable-shared-handles disable inter-process shared handles], try_shared_handles=$enableval, try_shared_handles=yes)
AC_MSG_RESULT($try_shared_handles)
if test "x$try_shared_handles" != "xyes"; then
	AC_DEFINE(DISABLE_SHARED_HANDLES)
	AC_SUBST(DISABLE_SHARED_HANDLES)
fi

TARGET="unknown"
ACCESS_UNALIGNED="yes"

case "$host" in
#mips-sgi-irix5.* | mips-sgi-irix6.*) TARGET=MIPS; ACCESS_UNALIGNED="no";;
i*86-*-*) TARGET=X86; arch_target=x86;;
sparc*-*-*) TARGET=SPARC; arch_target=sparc; ACCESS_UNALIGNED="no";;
#alpha*-*-linux* | alpha*-*-osf*) TARGET=ALPHA; ACCESS_UNALIGNED="no";;
#m68k-*-linux*) TARGET=M68K;;
powerpc-*-linux* | powerpc-*-sysv*) TARGET=POWERPC; arch_target=ppc;;
arm-*-linux-* | armv4l-*-linux-*) TARGET=ARM; arch_target=arm; ACCESS_UNALIGNED="no";;
esac

if test ${TARGET} = unknown; then
	CFLAGS="$CFLAGS -DNO_PORT"
	AC_MSG_WARN("mono has not been ported to $host: some things may not work.")
fi

if test ${ACCESS_UNALIGNED} = no; then
	CPPFLAGS="$CPPFLAGS -DNO_UNALIGNED_ACCESS"
fi

AM_CONDITIONAL(MIPS_GCC, test ${TARGET}${ac_cv_prog_gcc} = MIPSyes)
AM_CONDITIONAL(MIPS_SGI, test ${TARGET}${ac_cv_prog_gcc} = MIPSno)
AM_CONDITIONAL(SPARC, test x$TARGET = xSPARC)
AM_CONDITIONAL(X86, test x$TARGET = xX86)
AM_CONDITIONAL(ALPHA, test x$TARGET = xALPHA)
AM_CONDITIONAL(M68K, test x$TARGET = xM68K)
AM_CONDITIONAL(POWERPC, test x$TARGET = xPOWERPC)
AM_CONDITIONAL(ARM, test x$TARGET = xARM)

AC_SUBST(arch_target)
AC_SUBST(CFLAGS)

AC_OUTPUT([
Makefile
mono/Makefile
mono/utils/Makefile
mono/metadata/Makefile
mono/dis/Makefile
mono/cil/Makefile
mono/arch/Makefile
mono/os/Makefile
mono/os/win32/Makefile
mono/os/unix/Makefile
mono/arch/x86/Makefile
mono/arch/ppc/Makefile
mono/arch/sparc/Makefile
mono/arch/arm/Makefile
mono/interpreter/Makefile
mono/tests/Makefile
mono/benchmark/Makefile
mono/monoburg/Makefile
mono/monograph/Makefile
mono/jit/Makefile
mono/io-layer/Makefile
mono/handles/Makefile
runtime/Makefile
scripts/Makefile
man/Makefile
doc/Makefile
docs/Makefile
])

echo "

	GC:	$gc

"
